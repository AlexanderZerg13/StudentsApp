apply plugin: 'com.android.application'
apply plugin: 'io.fabric'


// Manifest version information!
def versionMajor = 1
def versionMinor = 0
def versionPatch = 9
def versionBuild = 1

android {
    archivesBaseName = "university"
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        applicationId "ru.infocom.university"

        buildConfigField ("int", 'DEMO_UNIVERSITY_ID', '1')
    }

    signingConfigs {
        release {
            Properties signingProperties = new Properties()
            def pFile = rootProject.file('signing/signing.properties')
            if (pFile.exists()) {
                signingProperties.load(new FileInputStream(pFile))
                storeFile rootProject.file("signing/" + signingProperties.storeFile)
                storePassword signingProperties.storePassword
                keyAlias signingProperties.keyAlias
                keyPassword signingProperties.keyPassword
            }
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            versionNameSuffix ' dev'
            ext.enableCrashlytics = false
        }
    }

    aaptOptions {
        additionalParameters "--no-version-vectors"
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:27.0.1'
    compile 'com.android.support:design:27.0.1'
    compile 'com.android.support:recyclerview-v7:27.0.1'
    compile 'com.android.support:preference-v7:27.0.1'
    compile 'com.afollestad.material-dialogs:core:0.9.4.7'
    compile 'com.maksim88:PasswordEditText:v0.8'

    compile 'com.squareup.okhttp3:logging-interceptor:3.4.1'
    compile 'com.squareup.okhttp3:okhttp:3.8.0'
    compile 'com.squareup.okhttp3:okhttp-urlconnection:3.6.0'

    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile "com.squareup.retrofit2:adapter-rxjava:2.1.0"
    compile ('com.squareup.retrofit2:converter-simplexml:2.1.0'){
        exclude group: 'stax', module: 'stax-api'
        exclude group: 'stax', module: 'stax'
        exclude group: 'xpp3', module: 'xpp3'
    }

    compile "io.reactivex:rxandroid:1.2.1"
    compile "io.reactivex:rxjava:1.2.1"
    compile "ru.arturvasilov:rx-loader:0.1.3"
    compile 'com.google.code.gson:gson:2.8.1'

    compile('com.mikepenz:materialdrawer:5.4.0@aar') {
        transitive = true
    }
    compile 'com.android.support:cardview-v7:27.0.1'
    compile 'com.bignerdranch.android:recyclerview-multiselect:0.2'
    compile 'commons-codec:commons-codec:1.10'
    compile('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
        transitive = true
    }
}
